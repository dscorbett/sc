!#############################################################################

! This is a sample rules file to illustrate David Corbett's Sound Changer.
! This file is based on Geoff Eddy's spanish.sc included with SCA 0.5.

!#############################################################################

! Change everything to lowercase.

lwc = a b c d e f g h i j k l m n o p q r s t u v w x y z
upc = A B C D E F G H I J K L M N O P Q R S T U V W X Y Z

<upc> > <lwc>$1

! Define some classes of vowel phonemes.

vowel = i e E a O o u
longv = í é É á Ó ó ú
front = i e E
back = u o O
lomid = E O
mid = e o
high = i u

! Define some classes of consonant phonemes.
! c C q are used for Old Spanish /ts tS dz/.

ustop  = p t k
vstop  = b d g
vfric  = B D G
ufric  = f T x
usib   = c s S
vsib   = q z Z
nasal  = m n N
liquid = l r R
glide  = j w
alv    = t c C s d q z l n r
lab    = p b v
res    = l r n s

! when one of these consonants is final, stress is normally on final syllable

acons = b c d f g h j k l m p q r t v w x z B C D F G H J K L M P Q R T W X Z

! These classes are combinations of others.

fric  = <vfric> <ufric>
uobs  = <ustop> <usib>
vobs  = <vstop> <vsib>
obs   = <uobs> <vobs>
cons  = <obs> <nasal> <liquid> <fric> <glide>
cnotl = <obs> <nasal> <fric>

!#############################################################################

! First of all, convert Latin spelling to the phonemic representation.

c|q > k
x > ks
y > i
ph > f

! Lose final /m/ and initial /h/.

m > / _ #
h > / # _

! Reduction of diphthongs and creation of prevocalic glides.

ae > E:
oe > e:
au > o:
<high> > <glide>$1 / _ <vowel>
e > j / _ <vowel>

!#############################################################################

! Assign the stress, indicated with a preceding ';', to the correct
! vowel. The antipenultimate is provisionally stressed; this stress is
! removed if the penultimate should be stressed.

! added to account for words of two short syllables.  ASh 2003 May 24

> ; / # <cons>* _ <vowel>

> ; / _ <vowel> :? <cons>* <vowel> <cons>* <vowel> <cons>* #
> ; / _ <vowel> : <cons>* <vowel>+ <cons>* #
> ; / _ <vowel> <cons>{2,} <vowel>+ <cons>* #
; > / _ .* ; [r]

! Convert from 'a:' to 'á'.

<vowel> > <longv>$1 / _ :
: >

! Vulgar Latin vowel changes 1: short vowels get lowered.

i > é
u > ó
e > E
o > O
<longv> > <vowel>$1

! Vulgar Latin vowel changes 2: /E O/ merge with /e o/.
! The '@' is necessary to preserve the vowel when stressed;
! this is a bit of a crock.

;E > @
E > e
@ > ;E
;O > @
O > o
@ > ;O

!#############################################################################

! Merger of /b/ and /v/.

v > B
b > B / <vowel> _ ;? <vowel>

! Palatisation of velars before fromt vowels.

sk > cc / _ ;? <front>+j
kk > cc / _ ;? <front>+j
k > c / _ ;? <front>+j
g > J / _ ;? <front>+j
j > / c+J _

! Loss of intertonic vowels in various environments.
! -- Too much? (Anton)

<mid>|<high> > / ; <vowel> <res> _ <cons>+ <vowel>
<mid>|<high> > / ; <vowel> <cons> _ <res> <vowel>
<mid>|<high> > / <vowel> <res> _ <cons> ; <vowel>
<mid>|<high> > / <vowel> <cons> _ <res> ; <vowel>

! Syllable-final velars become /j/.

k > j / . _ t|l|s
g > j / . _ n|l

! Simplification of various clusters:
! pt ps rs ns mn mb > tt ss ss s nn mb

p > t / _ t
p > s / _ s
r > s / _ s
n > / _ s
m > n / _ n
n > m / _ b

! More palatisations.

ptj > cc
ktj > cc
skj > cc
kkj > cc
tj > c
kj > c

!#############################################################################

! Raising and diphthongisation of some vowels.

o > u / _ n? j
<mid> > <high>$1 / _ <lab> j
<mid> > <high>$1 / _ <cons> <cons> j
<lomid> > <mid>$1 / _ <cons>+ j
;E > j;e
;O > w;e

! A few more palatisations.

jt > C
js > S
f > h / #|<vowel> _ <glide>? ;? <vowel>
j > J / # _ ;? <vowel>

lj > L
n+j > N
dj > JJ
gj > JJ

pj > jp / . _
rj > jr / . _
L > Z

! Tense resonants.

ll > L / <vowel> _ ;? <vowel>
nn > N / <vowel> _ ;? <vowel>
rr > R / <vowel> _ ;? <vowel>

!#############################################################################

! Lenition.

<vstop> > <vfric>$1 / <vowel>|<liquid> _ ;? <vowel>|<liquid>|<glide>
<vstop> > <vfric>$1 / <vowel>|<liquid> _ ;? <vowel>|<liquid>|<glide>
<uobs> > <vobs>$1 / <vowel>|<liquid> _ ;? <vowel>|<liquid>|<glide>
<uobs> > <vobs>$1 / <vowel>|<liquid> _ ;? <vowel>|<liquid>|<glide>
J > / <vowel>|<liquid> _ ;? <vowel>|<liquid>|<glide>
<obs> > / _ $1

! Initial Cl > L

pl > L / # _ ;? <vowel>
kl > L / # _ ;? <vowel>
fl > L / # _ ;? <vowel>

! Loss of remaining intertonic vowels.
! FIXME: too broad? (Anton)

<mid>|<high> > / ; <vowel> <cons> _ <cons>+ <vowel>
<mid>|<high> > / <vowel> <cons> _ <cons> ; <vowel>

! Some glide changes.

sj > js / <vowel> _
aw > o
ow > o
aj > e
ej > e
ij > i

! Final vowels.

u > o / _ #
i > e / _ #
e > / <vowel> <alv> _ #

! Some consonant cluster simplifications.

mt|nt > nd
nk > ng
L > l / _ <cons>
N > n / _ <cons>
R > r / _ <cons>

dn > n
ct > q
Dc > q
ptm > m
mm > m

! note: ndc also > nc

ndc > ng
rdc > rc
ntc > nc
mpt > nt
skp > sp
spt > st
stk > sk

! note: these also > lm, ngl

nm > rm
ngn > ngr

ndn > ndr
mn > mbr

tn > nd
tl > ld
nr > ndr
ml > mbl

<lab> > w / <vowel> _ <cons>
w > / <back> _
l > w / a _ q

t|d > q / . _ <cnotl>

b > u / _ d
B > u / _ <obs>
B > b

! FIXME: just _d?

!#############################################################################

! Devoicing and separation of sibilants.

J > j
jj > j

<vsib> > <usib>$1

c > T
S|Z > x

! Spellings!
! Much altered by Anton Sherwood, 2003 May 20

T > z
z > c / _ ;? <front>|j

j > y
L > ll
R > rr
N > ñ
w > hw / #|<vowel> _ ;? <vowel>

<vfric> > <vstop>$1
k > qu / _ ;? <front>|y
k > c
C > ch

x > j
j > g / _ ;? <front>|y

<vowel> > <longv>$1 / <vowel> .* ; _ n? s? #
<vowel> > <longv>$1 / ; _ .* <vowel> .* <vowel>
<vowel> > <longv>$1 / ; _ .* <vowel> <acons> #
; >
w > u
y > hi / # _ e
y > i / <cons> _
y > i / _ <cons>

> e / # _ s <cons>

!#############################################################################